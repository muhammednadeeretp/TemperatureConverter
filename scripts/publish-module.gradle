apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    }
    else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                }
                else {
                    from components.java
                }

                artifact androidSourcesJar
                // artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Temperature converter Android SDK'
                    url = 'https://github.com/muhammednadeeretp/TemperatureConverter'
                    licenses {
                        license {
                            name = 'Apache License'
                            url = 'https://github.com/muhammednadeeretp/TemperatureConverter/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'muhammed.nadeer'
                            name = 'Muhammed Nadeer'
                            email = 'muhammed.nadeer@zcocorporation.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here  https://github.com/muhammednadeeretp/TemperatureConverter.git
                    scm {
                        connection = 'scm:git:github.com/muhammednadeeretp/TemperatureConverter.git'
                        developerConnection = 'scm:git:ssh://github.com/muhammednadeeretp/TemperatureConverter.git'
                        url = 'https://github.com/muhammednadeeretp/TemperatureConverter'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}

/*
signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}*/
